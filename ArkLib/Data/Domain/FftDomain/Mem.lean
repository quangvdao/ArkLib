/-
Copyright (c) 2024-2026 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Sutherland, Ilia Vlasov, Aristotle (Harmonic)
-/
module

public import Mathlib.GroupTheory.SpecificGroups.Cyclic
public import Mathlib.Algebra.Group.TypeTags.Basic
public import Mathlib.Algebra.Group.Defs
public import Mathlib.Tactic.Cases
public import Mathlib.Tactic.Linarith
public import Mathlib.Tactic.LinearCombination
public import Mathlib.Tactic.Field

public import ArkLib.Data.Domain.CosetFftDomain.Mem
public import ArkLib.Data.Domain.FftDomain.Defs
public import ArkLib.ToMathlib.Finset.ToListWithProof

/-!
# Membership in FFT domains

This file develops membership lemmas for FFT domains and relates them to the
corresponding coset FFT domain constructions.

## Main results

- `FftDomainClass.one_mem`: The element `1` belongs to every FFT domain.
- `FftDomain.mem_iff_exists`: Membership via the subgroup parametrization.
- `FftDomain.mem_iff_mem_toCosetFftDomain`:
  Membership agrees with the associated coset FFT domain.
- `FftDomain.mem_toFinset_iff_mem`:
  Membership agrees with membership in the finset of elements.
-/

@[expose] public section

namespace Domain

variable {ι : Type} [AddCommGroup ι]
variable {F : Type} [Field F]

namespace FftDomainClass

variable {D : Type} [FunLike D ι F] [FftDomainClass D ι F]
variable {ω : D}

/-- The element `1` belongs to every FFT domain. -/
@[simp]
lemma one_mem : 1 ∈ ω := ⟨0, FftDomainClass.generator_eq_one ω⟩

/-- Membership in a FFT domain is decidable
  via membership in the finset of its elements. -/
instance [Fintype ι] [DecidableEq F] {x : F} : Decidable (x ∈ ω) :=
  decidable_of_iff _ CosetFftDomainClass.mem_toFinset_iff_mem

end FftDomainClass

namespace FftDomain

open CosetFftDomain

variable {ω : FftDomain ι F} {x : F}

/-- Membership in a concrete FFT domain means
  being one of the values of its subgroup parametrization. -/
lemma mem_iff_exists :
    x ∈ ω ↔ ∃ i, x = ω.subgroupDomain i := by
  aesop (add simp [Membership.mem])

/-- Membership in an FFT domain is the same as
  membership in the same domain viewed as a coset FFT domain. -/
lemma mem_iff_mem_toCosetFftDomain :
    x ∈ ω ↔ x ∈ ω.toCosetFftDomain := by
  simp [mem_iff_exists, mem_iff_exists_mul, ω.cosetGenerator_one]

/-- Membership in the image finset of an FFT domain means
  being one of the values of its subgroup parametrization. -/
lemma mem_toFinset_iff_exists [Fintype ι] [DecidableEq F] :
    x ∈ ω.toFinset ↔ ∃ i, x = ω.subgroupDomain i := by
  aesop
    (add simp
      [CosetFftDomainClass.mem_toFinset_iff_mem,
       CosetFftDomainClass.mem_def])

/-- Membership in the finset of elements is the same as membership in the FFT domain. -/
@[simp]
lemma mem_toFinset_iff_mem [Fintype ι] [DecidableEq F] :
    x ∈ ω.toFinset ↔ x ∈ ω := by
  rw [CosetFftDomainClass.mem_toFinset_iff_mem,
      mem_iff_mem_toCosetFftDomain]

end FftDomain

end Domain
