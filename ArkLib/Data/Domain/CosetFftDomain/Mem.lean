/-
Copyright (c) 2024-2026 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Julian Sutherland, Ilia Vlasov, Aristotle (Harmonic)
-/
module

public import Mathlib.GroupTheory.SpecificGroups.Cyclic
public import Mathlib.Algebra.Group.TypeTags.Basic
public import Mathlib.Algebra.Group.Defs
public import Mathlib.Tactic.Cases
public import Mathlib.Tactic.Linarith
public import Mathlib.Tactic.LinearCombination
public import Mathlib.Tactic.Field

public import ArkLib.Data.Domain.CosetFftDomain.Defs
public import ArkLib.ToMathlib.Finset.ToListWithProof

/-!
# Membership in coset FFT domains

This file develops membership and `toFinset` constructions for coset FFT
domains.

## Main definitions

- `Membership F D`: Membership in a domain via evaluation.
- `CosetFftDomainClass.toFinset`: The finset of elements of a domain.
- `CosetFftDomain.toFinset`: The finset of elements of a concrete coset FFT domain.

## Main results

- `CosetFftDomainClass.mem_def`: Characterization of membership.
- `CosetFftDomainClass.not_zero_mem`: Zero does not belong to a coset FFT domain.
- `CosetFftDomain.mem_iff_exists_mul`: Membership as a coset-generator multiple.
- `CosetFftDomainClass.card_toFinset`: Cardinality of the image finset.

-/

@[expose] public section

namespace Domain

open Function

variable {ι : Type} [AddCommGroup ι]
variable {F : Type} [Field F]

/-- Membership in a class-level coset FFT domain means
  being equal to one of the values of its indexing function. -/
instance {D : Type}
  [FunLike D ι F] [CosetFftDomainClass D ι F] : Membership F D where
  mem ω x := ∃ i, ω i = x

/-- A class-level coset FFT domain coerces to the type associated to its finset of elements. -/
instance
    {ι : Type} [AddCommGroup ι] [Fintype ι]
    {F : Type} [Field F] [DecidableEq F]
    {D : Type} [FunLike D ι F] [CosetFftDomainClass D ι F] :
  CoeSort D Type where
    coe d := CosetFftDomainClass.toFinset d

namespace CosetFftDomainClass

variable {D : Type} [FunLike D ι F] [CosetFftDomainClass D ι F] {ω : D}
variable {x : F}

/-- Unfold membership in a class-level coset FFT domain. -/
lemma mem_def : x ∈ ω ↔ ∃ i, ω i = x := by rfl

/-- Every value of a coset FFT domain belongs to that domain. -/
@[simp high]
lemma mem_self {i : ι} :
    ω i ∈ ω := by simp [mem_def]

/-- Membership is preserved by converting a class-level coset FFT domain
  to the concrete `CosetFftDomain` structure. -/
@[simp]
lemma mem_toCosetFftDomain_iff_mem :
    x ∈ toCosetFftDomain ω ↔ x ∈ ω := by
  simp only [mem_def, toCosetFftDomain_apply]

/-- Membership in the finset of elements is the same as membership in the coset FFT domain. -/
@[simp]
lemma mem_toFinset_iff_mem [Fintype ι] [DecidableEq F] :
    x ∈ toFinset ω ↔ x ∈ ω := by aesop (add simp [toFinset, mem_def])

/-- Every value of a coset FFT domain belongs to the set of its elements. -/
@[simp high]
lemma mem_toFinset_self [Fintype ι] [DecidableEq F] {i : ι} :
    ω i ∈ toFinset ω := by simp

lemma card_toFinset_le_fintype_card [Fintype F] [Fintype ι] [DecidableEq F] :
    Finset.card (toFinset ω) ≤ Fintype.card F := Finset.card_le_card (by simp)

/-- Zero is not a member of a coset FFT domain. -/
@[simp]
lemma not_zero_mem :
    0 ∉ ω := fun contra ↦ by
  rw [mem_def] at contra
  obtain ⟨i, contra⟩ := contra
  exact CosetFftDomainClass.ne_zero ω i (by simp_all)

@[simp]
lemma ne_zero_dep [Fintype ι] [DecidableEq F] (x : ω) :
    x.val ≠ 0 := fun contra ↦ by
  have := x.2
  simp_all

/-- The finset of elements of a coset FFT domain is inhabited.

  There always exists `ω 0`.
-/
instance [Fintype ι] [DecidableEq F] {ω : CosetFftDomain ι F} : Inhabited ω.toFinset where
  default := ⟨ω 0, by simp [CosetFftDomainClass.toFinset]⟩

/-- A concrete coset FFT domain coerced to `Type` is inhabited. -/
instance [Fintype ι] [DecidableEq F] {ω : CosetFftDomain ι F} : Inhabited ω where
  default := ⟨ω 0, by simp [CosetFftDomainClass.toFinset]⟩

/-- Membership in a coset FFT domain is decidable
  via membership in its finset of elements. -/
instance [Fintype ι] [DecidableEq F] : Decidable (x ∈ ω) :=
  decidable_of_iff _ mem_toFinset_iff_mem

end CosetFftDomainClass

namespace CosetFftDomain

variable {ω : CosetFftDomain ι F} {x : F}

/-- Membership in a concrete coset FFT domain means
  being a coset generator times some subgroup element. -/
lemma mem_iff_exists_mul :
    x ∈ ω ↔ ∃ i, x = ω.cosetGenerator * ω.subgroupDomain i := by
  aesop (add simp [Membership.mem])

/-- Membership in the finset of elements of a concrete coset FFT domain means
  being a coset generator times some subgroup element. -/
lemma mem_toFinset_iff_exists_mul [Fintype ι] [DecidableEq F] :
    x ∈ ω.toFinset ↔ ∃ i, x = ω.cosetGenerator * ω.subgroupDomain i := by
  simp [mem_iff_exists_mul]

/-- Membership in the finset of elements is
  the same as membership in the concrete coset FFT domain. -/
@[simp]
lemma mem_toFinset_iff_mem [Fintype ι] [DecidableEq F] :
    x ∈ ω.toFinset ↔ x ∈ ω := CosetFftDomainClass.mem_toFinset_iff_mem

/-- Every value of a concrete coset FFT domain belongs to its finset of elements. -/
@[simp high]
lemma mem_toFinset_self [Fintype ι] [DecidableEq F] {i : ι} :
    ω i ∈ ω.toFinset := CosetFftDomainClass.mem_toFinset_self

end CosetFftDomain

end Domain
