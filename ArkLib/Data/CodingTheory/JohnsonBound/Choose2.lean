/-
Copyright (c) 2024-2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ilia Vlasov, František Silváši
-/
module

public import Mathlib.Analysis.Convex.Function
public import Mathlib.Data.Set.Pairwise.Basic

public import ArkLib.Data.CodingTheory.Basic.DecodingRadius
public import ArkLib.Data.CodingTheory.Basic.Distance
public import ArkLib.Data.CodingTheory.Basic.LinearCode
public import ArkLib.Data.CodingTheory.Basic.RelativeDistance
/-! # Johnson Bound Choose-2 Lemmas -/

@[expose] public section


namespace JohnsonBound

open Finset

def f (x : ℚ) : ℚ := x ^ 2 - x

lemma f_convex {x₁ x₂ α₁ α₂ : ℚ}
    (h_nonneg_1 : 0 ≤ α₁) (h_nonneg_2 : 0 ≤ α₂) (h_conv : α₁ + α₂ = 1) :
    f (α₁ * x₁ + α₂ * x₂) ≤ α₁ * f x₁ + α₂ * f x₂ := by
  unfold f
  obtain ⟨rfl⟩ := show α₂ = 1 - α₁ by rw [← h_conv]; simp
  suffices 0 ≤ α₁ * (1 - α₁) * (x₁ - x₂) ^ 2 by linarith
  exact mul_nonneg (mul_nonneg h_nonneg_1 h_nonneg_2) (sq_nonneg _)

def choose_2 (x : ℚ) : ℚ := x * (x - 1) / 2

lemma choose_2_eq_half_f : choose_2 = (1 / 2) * f := by
  ext x; simp [choose_2, f]; ring

theorem choose_2_convex : ConvexOn ℚ Set.univ choose_2 := by
  rw [choose_2_eq_half_f]
  refine ⟨convex_univ, fun x₁ _ x₂ _ α₁ α₂ hα₁ hα₂ h ↦ ?_⟩
  have := f_convex (x₁ := x₁) (x₂ := x₂) hα₁ hα₂ h
  change (1 / 2 * f) (α₁ * x₁ + α₂ * x₂) ≤ α₁ * (1 / 2 * f) x₁ + α₂ * (1 / 2 * f) x₂
  simp only [Pi.mul_apply, Pi.div_apply, Pi.ofNat_apply]
  nlinarith [mul_le_mul_of_nonneg_left this (by norm_num : (0 : ℚ) ≤ 1 / 2)]

end JohnsonBound
